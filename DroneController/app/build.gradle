apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdkVersion 33
    ndkVersion "23.2.8568313"
    defaultConfig {
        applicationId "com.dotscene.dronecontroller"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 10       // updated by CI, ignore this value here
        versionName "v0.0.1" // updated by CI, ignore this value here
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file("../keystore.jks")
            storePassword System.getenv("DOTCUBE_APP_KEY_PWD")
            keyAlias "DroneController"
            keyPassword System.getenv("DOTCUBE_APP_KEY_PWD")
        }
    }
    buildTypes {
        no_network {
            debuggable true
            buildConfigField "boolean", "MOCK_NETWORK", "true"
            buildConfigField "boolean", "USE_CUSTOM_SERVER", "false"
            signingConfig signingConfigs.debug
        }
        custom_server {
            debuggable true
            buildConfigField "boolean", "MOCK_NETWORK", "false"
            buildConfigField "boolean", "USE_CUSTOM_SERVER", "true"
            signingConfig signingConfigs.debug
        }
        debug {
            debuggable true
            buildConfigField "boolean", "MOCK_NETWORK", "false"
            buildConfigField "boolean", "USE_CUSTOM_SERVER", "false"
        }
        release {
            buildConfigField "boolean", "MOCK_NETWORK", "false"
            buildConfigField "boolean", "USE_CUSTOM_SERVER", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        jniLibs {
            keepDebugSymbols += ['**/*.so']
        }
    }
    namespace 'com.dotscene.dronecontroller'
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.android:flexbox:1.0.0'
    implementation 'com.google.android.gms:play-services-vision:19.0.0'
    implementation 'com.github.nisrulz:qreader:2.1.2'
    implementation 'com.github.barteksc:android-pdf-viewer:2.8.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'

}

task assembleLicenseFile(type: Exec) {
    workingDir "$projectDir/"
    commandLine "python3", "$projectDir/../../dotcube-licenses/compile-licenses.py", "-b", "$projectDir/src/main/res/raw/licenses.dat"
}

preBuild.dependsOn assembleLicenseFile
